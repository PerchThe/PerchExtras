plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id("com.gradleup.shadow") version "9.3.0"

}

group = 'me.olziedev'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "olzie-repo"
        url = "https://repo.olziedev.com"
    }
    maven {
        name = "dmulloy2-repo"
        url = "https://repo.dmulloy2.net/nexus/repository/public/"
    }
    maven {
        name = "quickshop-repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "essentials-releases"
        url = "https://repo.essentialsx.net/releases/"
    }
    maven {
        name = "dv8tion"
        url = "https://m2.dv8tion.net/releases"
    }
    maven {
        name = "scarsz-nexus"
        url = "https://nexus.scarsz.me/content/groups/public/"
    }
    maven {
        name = "enginehub-maven"
        url = "https://maven.enginehub.org/repo/"
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    compileOnly "org.spigotmc:spigot:1.17.1-R0.1-SNAPSHOT"

    implementation "com.olziedev:olziecommand-minecraft:1.3.3"
    implementation "com.olziedev:olziemenu:1.0.0"

    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    compileOnly "com.github.TechFortress:GriefPrevention:16.18-RC1"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"
    compileOnly("org.maxgamer:QuickShop:5.0.0.16-SNAPSHOT") {
        transitive = false
    }
    compileOnly("com.discordsrv:discordsrv:1.25.1") {
        transitive = false
    }
    compileOnly("net.essentialsx:EssentialsX:2.19.0") {
        transitive = false
    }
    compileOnly("com.sk89q.worldguard:worldguard-bukkit:7.0.8-SNAPSHOT") {
        exclude group: 'org.bstats', module: 'bstats-bukkit'
    }
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21")
    }

    shadowJar {
        archiveFileName = "${project.name}-${project.version}-SHADED.jar"
        exclude 'META-INF/maven/**'
    }

    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
